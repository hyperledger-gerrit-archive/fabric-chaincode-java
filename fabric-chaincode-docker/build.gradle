
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.6'
    }
}

dependencies {
    compile project(':fabric-chaincode-shim')
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.*

task copyLib (type: Copy) {
    dependsOn ':fabric-chaincode-shim:build'
    from project(':fabric-chaincode-shim').configurations.runtime
    into('build/distributions/chaincode-java/lib')
}

task copyShimJar(type: Copy) {
    dependsOn copyLib
    from project(':fabric-chaincode-shim').jar
    into('build/distributions/chaincode-java/lib')
}

task copyStartScript(type: Copy) {
    dependsOn copyShimJar
    from ('start')
    into ('build/distributions/chaincode-java')
}


task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/Dockerfile')
    from 'hyperledger/fabric-baseimage:x86_64-0.4.7'
    maintainer 'Gennady Laventman "gennady@il.ibm.com"'
    addFile('distributions/', '/root/')

    instruction {'RUN mkdir -p /root/chaincode-java/chaincode'}
    instruction {'RUN chmod +x /root/chaincode-java/start'}

}

task buildImage(type: DockerBuildImage) {
    dependsOn copyStartScript
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tags = ['hyperledger/fabric-javaenv', 'hyperledger/fabric-javaenv:x86_64-latest', 'hyperledger/fabric-javaenv:x86_64-1.1.0']
}

