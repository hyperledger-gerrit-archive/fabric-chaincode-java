
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.6'
    }
}

dependencies {
    compile project(':fabric-chaincode-shim')
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.*

task copyLib (type: Copy) {
    dependsOn ':fabric-chaincode-shim:build'
    from project(':fabric-chaincode-shim').configurations.runtime
    // from('../fabric-chaincode-shim/build/distributions')
    into('build/distributions/chaincode-java/lib')
}

task copyShimJar(type: Copy) {
    dependsOn copyLib
    from project(':fabric-chaincode-shim').jar
    into('build/distributions/chaincode-java/lib')
}

task createDockerfile(type: Dockerfile) {
    dependsOn copyShimJar
    destFile = project.file('build/Dockerfile')
    from 'hyperledger/fabric-baseos:x86_64-0.4.6'
    maintainer 'Gennady Laventman "gennady@il.ibm.com"'
    addFile('distributions/', '/root/')
    instruction {'RUN mkdir -p /root/chaincode-java/chaincode'}
    instruction {'RUN mkdir -p /root/chaincode-java/bin'}
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = 'hyperledger/fabric-javaenv'
}

build.dependsOn buildImage

